//
// :sdk
//

allprojects {
    apply plugin: 'jacoco'

    jacoco {
        toolVersion '0.7.9'
    }

    //noinspection GroovyAssignabilityCheck
    group rootProject.ext.groupId
    //noinspection GroovyAssignabilityCheck
    version rootProject.ext.versionName
}

subprojects {
    apply plugin: 'com.android.library'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'com.github.dcendents.android-maven'

    def ext = rootProject.ext

    ext.sdkName = 'appcenter.android'

    android {

        defaultConfig {
            consumerProguardFiles 'proguard-rules.pro'

            buildConfigField 'int', "MIN_SDK_VERSION", "${ext.minSdkVersion}"
            buildConfigField 'int', "TARGET_SDK_VERSION", "${ext.targetSdkVersion}"
        }

        buildTypes {
            release {
                buildConfigField 'String', "SDK_NAME", "\"${ext.sdkName}\""
                minifyEnabled false
            }
            debug {
                buildConfigField 'String', "SDK_NAME", "\"${ext.sdkName}\""
                testCoverageEnabled true
            }
        }

        testOptions {
            unitTests {
                all {
                    jvmArgs '-noverify'
                }
                returnDefaultValues = true
            }
        }

        lintOptions {
            explainIssues true
            textReport true
        }
    }

    configurations {
        javadocDeps
    }

    dependencies {
        compileOnly "com.android.support:support-annotations:${ext.supportLibVersion}"
        javadocDeps "com.android.support:support-annotations:${ext.supportLibVersion}"

        testImplementation 'org.powermock:powermock-api-mockito:1.6.5'
        testImplementation 'org.powermock:powermock-module-junit4:1.6.5'
        testImplementation 'org.powermock:powermock-module-junit4-rule-agent:1.6.5'
        testImplementation "com.android.support:support-annotations:${ext.supportLibVersion}"
        testImplementation project(':test')

        androidTestImplementation 'com.crittercism.dexmaker:dexmaker-dx:1.4'
        androidTestImplementation 'com.crittercism.dexmaker:dexmaker-mockito:1.4'
        androidTestImplementation 'com.android.support.test:runner:1.0.1'
        androidTestImplementation 'com.android.support.test:rules:1.0.1'
        androidTestImplementation project(':test')
    }

    // Android Studio 2.2.0 broke a lot of scripting, could not find another way of doing this rename trick
    def createDebugCoverageReportExecutionDataFile = file("${buildDir}/outputs/code-coverage/connected/coverage.ec")

    task renameCreateDebugCoverageReportExecutionDataFile(dependsOn: 'createDebugCoverageReport') {
        doLast {
            def file = file(createDebugCoverageReportExecutionDataFile.getParentFile()).listFiles().first()
            file.renameTo(createDebugCoverageReportExecutionDataFile)
        }
    }

    task coverageReport(type: JacocoReport, dependsOn: ['renameCreateDebugCoverageReportExecutionDataFile', 'testDebugUnitTest']) {
        reports {
            xml.enabled = false
            html.enabled = true
        }

        def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*']
        def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
        def mainSrc = "${project.projectDir}/src/main/java"

        sourceDirectories = files([mainSrc])
        classDirectories = files([debugTree])
        executionData = files(["${buildDir}/jacoco/testDebugUnitTest.exec",
                               createDebugCoverageReportExecutionDataFile])
    }

    task sourcesJar(type: Jar) {
        afterEvaluate {
            from android.sourceSets.main.java.srcDirs
            classifier = 'sources'
        }
    }

    task javadoc(type: Javadoc) {
        afterEvaluate {
            source = android.sourceSets.main.java.srcDirs
            classpath += configurations.javadocDeps
            configurations.api.dependencies.withType(ProjectDependency).dependencyProject.buildDir.each { dir -> classpath += files("${dir}/intermediates/classes/release") }
            //noinspection GroovyAssignabilityCheck
            classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
            javadoc.dependsOn project.assembleRelease
        }
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        //noinspection GroovyAccessibility
        from javadoc.destinationDir
    }

    // This workaround is required only when using "org.gradle.configureondemand: true"
    bintrayUpload.dependsOn tasks.install

    artifacts {
        archives javadocJar
        archives sourcesJar
    }

    bintray {
        user = ext.bintrayUser
        key = ext.bintrayKey

        configurations = ['archives']

        publish = true

        pkg {
            repo = ext.bintrayRepo
            name = project.name
            userOrg = ext.bintrayUserOrg
            websiteUrl = ext.siteUrl
            vcsUrl = ext.gitUrl
            licenses = [ext.licenseCode]
        }
    }

    //noinspection GroovyMissingReturnStatement
    install {
        afterEvaluate {
            repositories.mavenInstaller {
                pom {
                    //noinspection GroovyAssignabilityCheck
                    project {
                        packaging 'aar'

                        //noinspection GroovyAssignabilityCheck
                        name project.name
                        description = project.description
                        url = ext.siteUrl

                        licenses {
                            license {
                                //noinspection GroovyAssignabilityCheck
                                name ext.licenseName
                                url ext.licenseSite
                            }
                        }
                        developers {
                            developer {
                                id ext.developerId
                                //noinspection GroovyAssignabilityCheck
                                name ext.developerName
                                email ext.developerEmail
                            }
                        }
                        scm {
                            connection ext.gitUrl
                            developerConnection ext.gitUrl
                            url ext.siteUrl
                        }
                    }
                    whenConfigured { generatedPom ->

                        // Eliminate test-scoped dependencies (no need in maven central poms).
                        generatedPom.dependencies.removeAll { dep ->
                            dep.scope == 'test'
                        }
                    }
                }
            }
        }
    }
}

// :sdk:coverageReport which combines all coverageReports generated by sub projects
task coverageReport(type: JacocoReport, dependsOn: subprojects.coverageReport) {
    reports {
        xml.enabled = true
        html.enabled = true
    }

    def coverageReports = subprojects.coverageReport.findAll { !it.project.name.contains("rum") }

    //noinspection GroovyAssignabilityCheck
    sourceDirectories = files(coverageReports.sourceDirectories)
    //noinspection GroovyAssignabilityCheck
    classDirectories = files(coverageReports.classDirectories)
    //noinspection GroovyAssignabilityCheck
    executionData = files(coverageReports.executionData)
}

apply plugin: 'com.github.kt3k.coveralls'

coveralls {
    jacocoReportPath "$buildDir/reports/jacoco/coverageReport/coverageReport.xml"
    sourceDirs = files(subprojects.coverageReport.sourceDirectories).files.absolutePath
}

tasks.coveralls.dependsOn coverageReport

task javadoc(type: Javadoc, dependsOn: subprojects.javadoc) {
    // Do nothing for :sdk but run the task for its children.
}

// Since Android Studio 2.2.0 we can't use sub project assembleRelease task dependency before the sub project evaluation
configure(subprojects.findAll { it.subprojects.isEmpty() }) {
    afterEvaluate {
        bintrayUpload.dependsOn assembleRelease
    }
}

task bintrayUpload(dependsOn: [subprojects.bintrayUpload]) {
    // Do nothing for :sdk but run the task for its children.
}

task clean(type: Delete) {
    delete project.buildDir
}